plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def namespaceName = 'com.tripleplaypay.magteksdk'
version = '0.6.0'

android {
    namespace 'com.tripleplaypay.magteksdk'
    compileSdk 34

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    publishing {
        singleVariant('release') { withSourcesJar() }
        singleVariant('debug') { withSourcesJar() }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation files('libs/mtscra.jar')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

static def thing(XmlProvider xml) {
    ((xml.asNode().get('dependencies') as NodeList).getAt(0) as Node).with {
        (appendNode 'dependency').with {
            appendNode 'groupId', 'com.tripleplaypay.magteksdk.magtek'
            appendNode 'artifactId', 'mt-scra'
            appendNode 'version', '99510109.1'
            appendNode 'scope', 'compile'
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = namespaceName
            artifactId = 'sdk-release'
            version = project.version

            afterEvaluate {
                from components.release
            }
            pom.withXml { thing(it) }
        }
        debug(MavenPublication) {
            groupId = namespaceName
            artifactId = 'sdk-debug'
            version = project.version

            afterEvaluate {
                from components.debug
            }
            pom.withXml { thing(it) }
        }
    }

    if (findProperty('githubTppUsername') != null) {
        repositories.maven {
            name 'Tpp'
            url 'https://maven.pkg.github.com/TriplePlayPay/TPP-Android-MagTekSDK'
            credentials {
                username "${githubTppUsername}"
                password "${githubTppToken}"
            }
        }
    }
}
